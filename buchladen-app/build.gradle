dependencies {
    def springDocVersion= '1.5.4'

    implementation project(':buchladen-api')
    implementation "org.springdoc:springdoc-openapi-data-rest:$springDocVersion"
    implementation "org.springdoc:springdoc-openapi-ui:$springDocVersion"
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // cdct
    contractTestImplementation "au.com.dius.pact.provider:junit5:${pactVersion}"
    contractTestImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

test {
    useJUnitPlatform()
}

pact {
    broker {
        pactBrokerUrl = 'http://localhost:8421'
    }
}

task contractTest(type: Test) {
    group 'verification'
    description = 'Run consumer driven contract tests'

    systemProperties System.properties
//    systemProperty 'pact.rootDir', "${project.rootDir}/pacts"
    systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults', 'false')
    systemProperty 'pact.provider.tag', System.getProperty('pact.provider.tag', 'local-check')
    systemProperty 'pact.provider.version', version.toString()

    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath =       sourceSets.contractTest.runtimeClasspath

    useJUnitPlatform()
}